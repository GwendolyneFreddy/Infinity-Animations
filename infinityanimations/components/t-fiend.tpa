/* ================================================== *
 *  INFINITY ANIMATIONS : Distinctive Fiends routine  *
 * ================================================== */

SILENT
/* DEPRECATED as of SCSII v13: "Avoided a possible clash with other mods by replacing the Abishai in Diathya's group in a more compatibility-friendly way"
// SCS2 overwrites an abishai with a nabassu
// -----------------------------------------
OUTER_SPRINT abi ~demabi01~
OUTER_SPRINT nab ~t-none~
ACTION_IF FILE_EXISTS_IN_GAME ~dw#nabsu.cre~ BEGIN
	COPY_EXISTING ~demabi01.cre~ ~override~
		PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			PATCH_IF BYTE_AT 0x27b = 0x33 BEGIN // If chaotic evil
			SPRINT abi ~t-none~
			SPRINT nab ~demabi01~
			END
		END
	BUT_ONLY
END
*/

/* ================================================ *
 *  Nabassus get the green-gray tanar'ri animation  *
 * ================================================ */
ACTION_FOR_EACH nb IN ~abydem01~ ~barbe2~ ~barbe3~ ~caddem13~ ~caddem5~ ~demnab01~ ~demnab02~ ~demnabr1~ ~demnabr2~ ~demnabsu~ ~dw#nabsu~ ~enddem04~ ~enddem05~ ~esp04~ ~f_demdrg~ ~finnabas~ ~obsdem01~ ~obsdem05~ ~pmaster~ ~ppnab01~ ~ppnab02~ ~ppnab03~ ~rumar02~ ~%tsu%tanar~ ~tanari01~ ~tg#dem2~ ~udnaba~ ~z#gnab01~ ~z#gnabas~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~FIEND_GREEN~) // classical 0x1001 - EE 0x1103
				WRITE_BYTE 0x2d 36						// Minor color (LIGHT_DIRTY_YELLOW)
				WRITE_BYTE 0x2e 20						// Major color (DARK_MOLDY_GREEN)
				WRITE_BYTE 0x2f 20						// Skin color
				PATCH_IF BYTE_AT 0x234 < 13 BEGIN
					WRITE_BYTE 0x234 13					// Level 1
				END
				WRITE_BYTE 0x237 1						// Sex (male)
				WRITE_BYTE 0x271 255					// General (monster)
				WRITE_BYTE 0x272 121					// Race (fiend)
				WRITE_BYTE 0x273 160					// Class (tanar'ri)
				READ_BYTE 0x275 gd						// Gender
				PATCH_IF (gd != 6) AND (gd != 9) BEGIN	// If not summoned
					WRITE_BYTE 0x275 1					// male
				END
				WRITE_BYTE 0x27b 51						// Alignment (chaotic evil by definition)
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 51		// Strong-dark by RGB
				END
				SAY INITIAL_MEETING @506				// Who disturbs me? [T-NAB04]
				SAY MORALE @507							// Rawrr! [T-NAB03]
				SAY UNHAPPY_BREAKING @507				// Rawrr! [T-NAB03]
				SAY BATTLE_CRY1 @508					// I will rend your soul! [T-NAB05]
				SAY BATTLE_CRY2 @509					// Your pain shall be eternal! [T-NAB06]
				WRITE_LONG BATTLE_CRY3 (0 - 1)
				WRITE_LONG BATTLE_CRY4 (0 - 1)
				WRITE_LONG BATTLE_CRY5 (0 - 1)
				WRITE_LONG ATTACK1 (0 - 1)
				WRITE_LONG ATTACK2 (0 - 1)
				WRITE_LONG DAMAGE (0 - 1)
				WRITE_LONG DYING (0 - 1)
				SAY SELECT_COMMON1 @507					// Rawrr! [T-NAB03]
				SAY SELECT_COMMON2 @506					// Who disturbs me? [T-NAB04]
				WRITE_LONG SELECT_COMMON3 (0 - 1)
			END
		BUT_ONLY
	END
END

/* ================================================ *
 *  Chromatic Demon (makes him somewhat chromatic)  *
 * ================================================ */
COPY_EXISTING ~gorchr.cre~ ~override~
	PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
	PATCH_IF valid BEGIN
		PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
		WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~FIEND_BLUE~) // classical 0x1002 - EE 0x1102
		WRITE_BYTE 0x2d 50		// Minor color (DARK_YELLOW)
		WRITE_BYTE 0x2e 58		// Major color (DARK_BLUE)
		WRITE_BYTE 0x2f 58		// Skin color
		WRITE_BYTE 0x275 1		// Gender (male)
		WRITE_BYTE 0x273 160	// Class (tanar'ri)
	END
BUT_ONLY

/* ========================================== *
 *  Bebiliths get the bebilith_nwn animation  *
 * ========================================== */
ACTION_FOR_EACH nb IN ~abaia~ ~abaia2~ ~f_demspi~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_SHORT 0x28 0x5032 // Animation ID (bebilith_nwn)
//				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~BEBILITH_NWN~) // classical 0x5032 - EE none for the moment
				READ_BYTE 0x237 sx		// Sex
				PATCH_IF sx > 4 BEGIN	// If invalid
					WRITE_BYTE 0x237 2	// female
				END
				WRITE_BYTE 0x271 255	// General (monster)
			END
		BUT_ONLY
	END
END

/* =========================================== *
 *  Bone Fiends get the osyluth_nwn animation  *
 * =========================================== */
ACTION_FOR_EACH nb IN ~cbgrbone~ ~dbonef01~ ~dw#vibon~ ~gorbat5~ ~melsum02~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_SHORT 0x28 0x5271 // Animation ID (osyluth_nwn)
//				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~OSYLUTH_NWN~) // classical 0x5271 - EE none for the moment
				WRITE_BYTE 0x2d 83		// Minor color (DARK_BLUISH_GRAY)
				WRITE_BYTE 0x2e 28		// Major color (BLUISH_SILVER)
				WRITE_BYTE 0x2f 28		// Skin color
				WRITE_BYTE 0x272 121	// Race (fiend)
				WRITE_BYTE 0x273 179	// Class (devil)
				WRITE_BYTE 0x275 4		// Gender (neither)
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 51 // Strong-dark by RGB
				END
			END
		BUT_ONLY
	END
END

/* ============================================= *
 *  Gelugons get the salamander_frost animation  *
 * ============================================= */
ACTION_FOR_EACH nb IN ~demgelsu~ ~gelugeon~ ~gelugon1~ ~gordem2~ ~rr#hgel~ ~rr#sgelu~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/t-cre_fixer.tpp~ // These CREs need fixing because of hosed slots
			WRITE_SHORT 0x28 0xe910					// Animation ID (salamander_frost)
			WRITE_BYTE 0x2f 57						// Skin color (LIGHT_BLUE)
			WRITE_BYTE 0x237 1						// Sex (male)
			WRITE_BYTE 0x271 255					// General (monster)
			READ_BYTE 0x275 gd						// Gender
			PATCH_IF (gd != 6) AND (gd != 9) BEGIN	// If not summoned
				WRITE_BYTE 0x275 1					// male
			END
			SAY BATTLE_CRY1 ~~ [T-GEL01A]
			SAY BATTLE_CRY2 ~~ [T-GEL01B]
			SAY BATTLE_CRY3 ~~ [T-GEL01C]
			SAY ATTACK1 ~~ [T-GEL03]
			SAY DAMAGE ~~ [T-GEL07]
			SAY DYING ~~ [T-GEL09]
			SAY SELECT_COMMON1 ~~ [T-GEL02A]
			SAY SELECT_COMMON2 ~~ [T-GEL02B]
			SAY SELECT_COMMON3 ~~ [T-GEL02C]
		BUT_ONLY
	END
END

/* ========================================== *
 *  Gelugons get the marilith_dark animation  *
 * ========================================== */
ACTION_FOR_EACH nb IN ~garmari1~ ~garmari2~ ~godmari1~ ~psmari1~ ~psmari2~ ~psmarile~ ~z_2spe2~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			WRITE_SHORT 0x28 0x5052		// Animation ID (marilith_dark)
//			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~MARILITH_DARK~) // classical 0x5052 - EE none for the moment
			WRITE_BYTE 0x2d 56			// Minor color (DARK_TURQUOISE)
			WRITE_BYTE 0x2e 63			// Major color (LIGHT_GRAY)
			WRITE_BYTE 0x2f 63			// Skin color
			WRITE_BYTE 0x237 2			// Sex (female)
			WRITE_BYTE 0x271 255		// General (monster)
			WRITE_BYTE 0x275 2			// Gender (female)
			SAY MORALE #61765			// No! Nooo! I must be away! [UNEARF08]
			SAY UNHAPPY_BREAKING #61765	// No, no, I must be away! [UNEARF08]
			SAY BATTLE_CRY1 #61761		// I shall enjoy gutting you like the dog you are... [UNEARF04]
			SAY BATTLE_CRY2 #61760		// Death will only be the beginning for you... [UNEARF03]
			SAY BATTLE_CRY3 #61762		// Now the time has come for you to pay... [UNEARF05]
			SAY SELECT_COMMON1 #61758	// Your death lies here, fool! [UNEARF01]
			SAY SELECT_COMMON2 #61759	// This is your end, I assure you. [UNEARF02]
			END
		BUT_ONLY
	END
END

/* ======================================== *
 *  Night Hags get the hag_night animation  *
 * ======================================== */
ACTION_FOR_EACH nb IN ~nighthag~ ~pshag~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ AND FILE_EXISTS_IN_GAME ~3akg1.bam~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~HAG_NIGHT~) // classical 0x5b0a - EE 0x7f5c
				WRITE_BYTE 0x2d 43			// Minor color (DARK_BEIGE)
				WRITE_BYTE 0x2e 65			// Major color (REALLY_DARK_GRAY)
				WRITE_BYTE 0x2f 18			// Skin color (BLUE)
				WRITE_BYTE 0x32 64			// Hair color (DARK_GRAY)
				WRITE_BYTE 0x237 2			// Sex (female)
				WRITE_BYTE 0x271 255		// General (monster)
				WRITE_BYTE 0x275 2			// Gender (female)
				WRITE_BYTE 0x272 121		// Race (fiend)
				WRITE_BYTE 0x27b 35			// Alignment (neutral evil by definition)
				SAY INITIAL_MEETING @510	// Well... hello, my pretty! [T-HAG01]
				SAY MORALE ~~ [T-HAG66B]
				SAY UNHAPPY_BREAKING ~~ [T-HAG66B]
				WRITE_LONG LEADER (0 - 1)
				WRITE_LONG TIRED (0 - 1)
				WRITE_LONG BORED (0 - 1)
				SAY BATTLE_CRY1 @511		// Your bones and blood will blanket my garden. [T-HAG77]
				SAY BATTLE_CRY2 ~~ [T-HAG66C]
				WRITE_LONG BATTLE_CRY3 (0 - 1)
				SAY ATTACK1 ~~ [T-HAG06A]
				SAY DAMAGE ~~ [T-HAG66A]
				SAY DYING @512				// Off with ye. Dead I am. [T-HAG90A]
				WRITE_LONG HURT (0 - 1)
				SAY SELECT_COMMON1 @513		// Do not push me! [T-HAG13]
				SAY SELECT_COMMON2 ~~ [T-HAG06B]
				WRITE_LONG SELECT_COMMON3 (0 - 1)
				WRITE_LONG SELECT_ACTION1 (0 - 1)
				WRITE_LONG SELECT_ACTION2 (0 - 1)
				WRITE_LONG SELECT_ACTION3 (0 - 1)
				WRITE_LONG SELECT_RARE1 (0 - 1)
				WRITE_LONG SELECT_RARE2 (0 - 1)
				WRITE_LONG CRITICAL_HIT (0 - 1)
				WRITE_LONG CRITICAL_MISS (0 - 1)
				WRITE_LONG TARGET_IMMUNE (0 - 1)
				WRITE_LONG INVENTORY_FULL (0 - 1)
				WRITE_LONG PICKED_POCKET (0 - 1)
				WRITE_LONG HIDDEN_IN_SHADOWS (0 - 1)
				SAY SPELL_DISRUPTED ~~ [T-HAG66B]
				WRITE_LONG SET_A_TRAP (0 - 1)
				WRITE_LONG EXISTANCE5 (0 - 1)
			END
		BUT_ONLY
	END
END

/* ==================================================================================== *
 *  Yochlols: Assigns yochlols a darkened otyugh animation (except those in drow form)  *
 * ==================================================================================== */
ACTION_FOR_EACH nb IN ~i#vyoch~ ~yachol~ ~yochlo01~ ~ysdemon~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~YOCHLOL~) // classical 0x5fdd - EE 0x7f43
				WRITE_BYTE 0x2d 60		// Minor color (DARK_PURPLE)
				WRITE_BYTE 0x2e 41		// Major color (MEDIUM_BROWN)
				WRITE_BYTE 0x2f 41		// Skin color
				WRITE_BYTE 0x237 2		// Sex (female)
				WRITE_BYTE 0x271 255	// General (monster)
				READ_BYTE 0x275 gd		// Gender
				PATCH_IF (gd != 6) AND (gd != 9) BEGIN // If not summoned
					WRITE_BYTE 0x275 2	// female
				END
				WRITE_BYTE 0x272 121	// Race (fiend)
				WRITE_BYTE 0x273 160	// Class (tanar'ri)
			END
		BUT_ONLY
	END
END

/* ================================================= *
 *  Silver Abishais get the abishai_white animation  *
 * ================================================= */
ACTION_FOR_EACH nb IN ~wardabi1~ ~wardabi2~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ABISHAI_WHITE~) // classical 0x5249 - EE 0xe28b
				WRITE_BYTE 0x2d 79		// Minor color (LIGHT_SILVER)
				WRITE_BYTE 0x2e 105		// Major color (OFF_WHITE)
				WRITE_BYTE 0x2f 105		// Skin color
				SAY BATTLE_CRY1 ~~ [T-ABI01A]
				SAY BATTLE_CRY2 ~~ [T-ABI02A]
				SAY BATTLE_CRY3 ~~ [T-ABI08A]
				SAY ATTACK1 ~~ [T-ABI03A]
				SAY DAMAGE ~~ [T-ABI07A]
				SAY DYING ~~ [T-ABI09A]
				SAY SELECT_COMMON1 ~~ [T-ABI01A]
				SAY SELECT_COMMON2 ~~ [T-ABI02A]
				SAY SELECT_COMMON3 ~~ [T-ABI04A]
				SAY SPELL_DISRUPTED ~~ [T-ABI10A]
			END
		BUT_ONLY
	END
END

/* ====================================== *
 *  Manes get the drowned_dead animation  *
 * ====================================== */
COPY_EXISTING ~mane.cre~ ~override~
              ~obsdem02.cre~ ~override~
	PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
		WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~DROWNED_DEAD~) // classical 0x5286 - EE 0xe25f
		WRITE_BYTE 0x272 121	// Race (fiend)
		WRITE_BYTE 0x273 160	// Class (tanar'ri)
	END
BUT_ONLY

/* ================================================ *
 *  Green Abishais get the abishai_green animation  *
 * ================================================ */
ACTION_FOR_EACH nb IN ~babi~ ~duegabis~ ~grenabi1~ ~remorhaz~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ABISHAI_GREEN~) // classical 0x5b05 - EE 0x7f59
				WRITE_BYTE 0x2d 51		// Minor color (DARK_OLIVE_GREEN)
				WRITE_BYTE 0x2e 54		// Major color (DARK_GREEN)
				WRITE_BYTE 0x2f 54		// Skin color
				SAY BATTLE_CRY1 ~~ [T-ABI01]
				SAY BATTLE_CRY2 ~~ [T-ABI02]
				SAY ATTACK1 ~~ [T-ABI03]
				SAY DAMAGE ~~ [T-ABI06]
				SAY DYING ~~ [T-ABI05]
				SAY SELECT_COMMON1 ~~ [T-ABI01]
				SAY SELECT_COMMON2 ~~ [T-ABI02]
				SAY SPELL_DISRUPTED ~~ [T-ABI04]
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 51 // Strong-dark by RGB
				END
			END
		BUT_ONLY
	END
END

/* ============================================ *
 *  Red Abishais get the abishai_red animation  *
 * ============================================ */
ACTION_FOR_EACH nb IN ~abishai1~ ~abisred1~ ~demabi01~ ~godabis1~ ~godabis2~ ~nsabish~ ~psbaat02~ ~psbaat2a~ ~rabi~ ~redabi01~ ~remorle~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ABISHAI_RED~) // classical 0x5b06 - EE 0x7f5a
				WRITE_BYTE 0x2d 51		// Minor color (DARK_OLIVE_GREEN)
				WRITE_BYTE 0x2e 46		// Major color (RED)
				WRITE_BYTE 0x2f 46		// Skin color
				SAY BATTLE_CRY1 ~~ [T-ABI01]
				SAY BATTLE_CRY2 ~~ [T-ABI02]
				SAY ATTACK1 ~~ [T-ABI04]
				SAY DAMAGE ~~ [T-ABI07]
				SAY DYING ~~ [T-ABI08]
				SAY SELECT_COMMON1 ~~ [T-ABI01]
				SAY SELECT_COMMON2 ~~ [T-ABI02]
				SAY SPELL_DISRUPTED ~~ [T-ABI03]
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 51 // Strong-dark by RGB
				END
			END
		BUT_ONLY
	END
END

/* ====================================== *
 *  Erinyes get wings and elf animations  *
 * ====================================== */
ACTION_IF NOT FILE_EXISTS_IN_GAME ~wings01.itm~ BEGIN
	COPY ~infinityanimations/wings~ ~override~

	COPY ~infinityanimations/items/wings01.itm~ ~override~
		SAY NAME1 @514 // Wings
		SAY NAME2 @514
		SAY UNIDENTIFIED_DESC @515 // These soft and feathered wings seem to be fashioned in a celestial design.
		SAY DESC @515
END

// Erinyes get the fighter_female_elf animation
// --------------------------------------------
ACTION_FOR_EACH nb IN ~deriny01~ ~gorbat2~ ~gorwom02~ ~jumjum~ ~psbaat03~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_SHORT 0x28 0x6111			// Animation ID (fighter_female_elf)
				WRITE_BYTE 0x2f 113				// Skin color (FOG)
				WRITE_BYTE 0x32 0				// Hair color (RUST-TINTED_BLACK)
				REPLACE_CRE_ITEM ~wings01~ #0 #0 #0 ~UNDROPPABLE~ ~HELMET~
				WRITE_BYTE 0x237 2				// Sex (female)
				WRITE_BYTE 0x275 2				// Gender (female)
				WRITE_BYTE 0x272 121			// Race (fiend)
				READ_BYTE 0x273 cs				// Class
				PATCH_IF cs > 20 BEGIN
					WRITE_BYTE 0x273 179		//devil
				END
				WRITE_BYTE 0x27b 19				// Alignment (lawful evil by definition)
				PATCH_IF SLONG_AT INITIAL_MEETING < 1 BEGIN
					SAY INITIAL_MEETING @516	// Well hello there. [T-ERI02A]
				END
				PATCH_IF SLONG_AT MORALE < 1 BEGIN
					SAY MORALE @517				// Farewell for now. [T-ERI06]
				END
				PATCH_IF SLONG_AT UNHAPPY_BREAKING < 1 BEGIN
					SAY UNHAPPY_BREAKING @517	// Farewell for now. [T-ERI06]
				END
				PATCH_IF SLONG_AT BATTLE_CRY1 < 1 BEGIN
					SAY BATTLE_CRY1 @518		// On your knees! [T-ERI01A]
					SAY BATTLE_CRY2 @519		// You are mine! [T-ERI01B]
					SAY BATTLE_CRY3 @520		// Rrrauugh! [T-ERI01C]
					SAY BATTLE_CRY4 @521		// You tempt pain that you have never dreamed of. [T-ERI01D]
					SAY BATTLE_CRY5 @522		// Rend their flesh! They will not need it. [T-ERI01E]
				END
				PATCH_IF SLONG_AT ATTACK1 < 1 BEGIN
					SAY ATTACK1 ~~ [T-ERI03]
				END
				PATCH_IF SLONG_AT DAMAGE < 1 BEGIN
					SAY DAMAGE ~~ [T-ERI07]
				END
				PATCH_IF SLONG_AT DYING < 1 BEGIN
					SAY DYING ~~ [T-ERI09]
				END
				PATCH_IF SLONG_AT HURT < 1 BEGIN
					SAY HURT @523				// You want to heal me. You must. [T-ERI10]
				END
				PATCH_IF SLONG_AT SELECT_COMMON1 < 1 BEGIN
					SAY SELECT_COMMON1 @516		// Well hello there. [T-ERI02A]
					SAY SELECT_COMMON2 @524		// I need to speak with you. Come here. [T-ERI02B]
					SAY SELECT_COMMON3 @525		// Come to my side. Help me here. [T-ERI02C]
					SAY SELECT_COMMON4 @526		// Oh, yes. [T-ERI02D]
					SAY SELECT_COMMON5 @527		// Ha! Ha-ha-ha... [T-ERI02E]
				END
				PATCH_IF SLONG_AT CRITICAL_HIT < 1 BEGIN
					SAY CRITICAL_HIT @528		// Oh, that is very nice. [T-ERI05]
				END
				PATCH_IF SLONG_AT CRITICAL_MISS < 1 BEGIN
					SAY CRITICAL_MISS @529		// No. No. [T-ERI08]
				END
				PATCH_IF SLONG_AT SPELL_DISRUPTED < 1 BEGIN
					SAY SPELL_DISRUPTED ~~ [T-ERI04]
				END
			END
		BUT_ONLY
	END
END

// Other Erinyes get the thief_female_elf
// --------------------------------------
ACTION_IF FILE_EXISTS_IN_GAME ~wardtief.cre~ BEGIN
	COPY_EXISTING ~wardtief.cre~ ~override~
		PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			WRITE_SHORT 0x28 0x6311							// Animation ID (thief_female_elf)
			REPLACE_CRE_ITEM ~wings01~ #0 #0 #0 ~UNDROPPABLE~ ~HELMET~
			WRITE_BYTE 0x272 121							// Race (fiend)
			FOR (s1 = 0xa4 ; s1 < 0x1d0 ; s1 += 4) BEGIN	// Clear soundset (is not Imoen)
				WRITE_LONG s1 (0 - 1)
			END
			SAY INITIAL_MEETING @516						// Well hello there. [T-ERI02A]
			SAY MORALE @517									// Farewell for now. [T-ERI06]
			SAY UNHAPPY_BREAKING @517						// Farewell for now. [T-ERI06]
			SAY BATTLE_CRY1 @518							// On your knees! [T-ERI01A]
			SAY BATTLE_CRY2 @519							// You are mine! [T-ERI01B]
			SAY BATTLE_CRY3 @520							// Rrrauugh!~ [T-ERI01C]
			SAY BATTLE_CRY4 @521							// You tempt pain that you have never dreamed of. [T-ERI01D]
			SAY BATTLE_CRY5 @522							// Rend their flesh! They will not need it. [T-ERI01E]
			SAY ATTACK1 ~~ [T-ERI03]
			SAY DAMAGE ~~ [T-ERI07]
			SAY DYING ~~ [T-ERI09]
			SAY HURT @523									// You want to heal me. You must. [T-ERI10]
			SAY SELECT_COMMON1 @516							// Well hello there. [T-ERI02A]
			SAY SELECT_COMMON2 @524							// I need to speak with you. Come here. [T-ERI02B]
			SAY SELECT_COMMON3 @525							// Come to my side. Help me here. [T-ERI02C]
			SAY SELECT_COMMON4 @526							// Oh, yes. [T-ERI02D]
			SAY SELECT_COMMON5 @527							// Ha! Ha-ha-ha... [T-ERI02E]
			SAY CRITICAL_HIT @528							// Oh, that is very nice. [T-ERI05]
			SAY CRITICAL_MISS @529							// No. No. [T-ERI08]
			SAY SPELL_DISRUPTED ~~ [T-ERI04]
		END
	BUT_ONLY
END

/* ======================================== *
 *  Succubi get the succubus_nwn animation  *
 * ======================================== */
ACTION_FOR_EACH nb IN ~demsuc01~ ~finsucc~ ~gorsuc01~ ~gortan2~ ~gorwom01~ ~nssuc01~ ~nssuc02~ ~pstana02~ ~pstana2a~ ~sd_hrzr2~ ~yugsuc01~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_SHORT 0x28 0x5290			// Animation ID (succubus_nwn)
//				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~SUCCUBUS_NWN~) // classical 0x5290 - EE none for the moment
				WRITE_BYTE 0x237 2				// Sex (female)
				WRITE_BYTE 0x275 2				// Gender (female)
				WRITE_BYTE 0x272 121			// Race (fiend)
				READ_BYTE 0x273 cs				// Class
				PATCH_IF cs > 20 BEGIN
					WRITE_BYTE 0x273 160		// tanar'ri
				END
				PATCH_IF SLONG_AT INITIAL_MEETING < 1 BEGIN
					SAY INITIAL_MEETING @530	// Yes? [T-SUC02]
				END
				PATCH_IF SLONG_AT BATTLE_CRY1 < 1 BEGIN
					SAY BATTLE_CRY1 @531		// Your death shall be beautiful! [T-SUC01A]
					SAY BATTLE_CRY2 @532		// Your soul shall be mine! [T-SUC01B]
				END
				PATCH_IF SLONG_AT DYING < 1 BEGIN
					SAY DYING ~~ [T-SUC09]
				END
				PATCH_IF SLONG_AT SELECT_COMMON1 < 1 BEGIN
					SAY SELECT_COMMON1 @530		// Yes? [T-SUC02]
				END
			END
		BUT_ONLY
	END
END

/* =========================================== *
 *  Kirinhale gets the succubus_nwn animation  *
 * =========================================== */
ACTION_IF FILE_EXISTS_IN_GAME ~%tsu%kirinh.cre~ BEGIN
	COPY_EXISTING ~%tsu%kirinh.cre~ ~override~
		PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			WRITE_SHORT 0x28 0x5290 // Animation ID (succubus_nwn)
//			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~SUCCUBUS_NWN~) // classical 0x5290 - EE none for the moment
			PATCH_IF BYTE_AT 0x234 < 6 BEGIN
			WRITE_BYTE 0x234 6		// Level 1 (was 0)
			END
			WRITE_BYTE 0x237 2		// Sex (female)
			WRITE_BYTE 0x23e 25		// Charisma (was 30)
			WRITE_BYTE 0x275 2		// Gender (female)
			WRITE_BYTE 0x272 121	// Race (fiend)
		END
	BUT_ONLY
END

/* ===================================== *
 *  Maurezhi get the maurezhi animation  *
 * ===================================== */
ACTION_FOR_EACH nb IN ~demmau01~ ~gortan6~ ~obsdem04~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~MAUREZHI~) // classical 0x547a - EE 0x7601
				WRITE_BYTE 0x2d 107		// Minor color (KHAKI_GRAY)
				WRITE_BYTE 0x2e 62		// Major color (DARK_LAVENDER)
				WRITE_BYTE 0x2f 62		// Skin color
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 51 // Strong-dark by RGB
				END
				SAY INITIAL_MEETING #61750	// This place will be your tomb! [UNEARM01]
				SAY MORALE #61757			// No! I must flee! [UNEARM08]
				SAY UNHAPPY_BREAKING #61757	// No, I must flee! [UNEARM08]
				SAY BATTLE_CRY1 #61752		// You'll suffer before you die, I promise. [UNEARM03]
				SAY BATTLE_CRY2 #61754		// I will be pleased to carve out your heart! [UNEARM05]
				SAY DAMAGE #61755			// [UNEARM06]
				SAY DYING #61756			// [UNEARM07]
				SAY SELECT_COMMON1 #61750	// This place will be your tomb! [UNEARM01]
				SAY SELECT_COMMON2 #61751	// You should not have come to this place! [UNEARM02]
			END
		BUT_ONLY
	END
END

/* ===================================================================== *
 *  Assigns glabrezus the proper animation instead of balor or cornugon  *
 * ===================================================================== */
ACTION_FOR_EACH nb IN ~demgla01~ ~demglab~ ~demglab2~ ~demglasu~ ~eastdem1~ ~eastdem3~ ~eastdem5~ ~eastdem6~ ~eastdem8~ ~enddem01~ ~enddem02~ ~gglab~ ~ily5~ ~jondem01~ ~jondem03~ ~jondem05~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN // Glabrezus
		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/t-cre_fixer.tpp~	// These CREs need fixing because of hosed slots
			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~GLABREZU~)		// classical 0xe0f1 - EE 0xe0f1 (0xed19 in IWD2)
			WRITE_BYTE 0x237 1		// Sex (male)
			WRITE_BYTE 0x271 255	// General (monster)
			READ_BYTE 0x275 gd		// Gender
			PATCH_IF (gd != 6) AND (gd != 9) BEGIN // If not summoned
				WRITE_BYTE 0x275 1	// male
			END
		BUT_ONLY
	END
END

VERBOSE
