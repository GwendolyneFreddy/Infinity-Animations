/* ======================================================================================================================================================================= *
 *  v 6.0.0                                                                                                                                                                *
 *                                     C A M B I O N S   G E T   I S A I R   A N I M A T I O N   C O M P O N E N T   ( 2 1 0 - 2 3 5 )                                     *
 *                                                                                                                                                                         *
 * ======================================================================================================================================================================= *
 *  This component assigns assigns Cambions creatures the Isair animation. Weapon changes necessary as Isair animation displays hardcoded scimitars.                       *
 * ======================================================================================================================================================================= *
 *  change-log:                                                                                                                                                            *
 *  -----------                                                                                                                                                            *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_cambions_arrays.tph library) for easier maintenance.                                   *
 *      - Rewrote animation ID patching to solve EE games compatibility: replaced WRITE_SHORT 0x28 0x5242 with WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ISAIR~).            *
 *      - Removed deprecated Male Tieflings patching process.                                                                                                              *
 * ======================================================================================================================================================================= */

SILENT

COPY_EXISTING ~%nb%.cre~ ~override~

	PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
	PATCH_IF valid BEGIN
		PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
		// If Cambion gets the Isair animation
		PATCH_IF rgc = 1 BEGIN
			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ISAIR~) // original 0x5242 - EE 0xe275
			WRITE_BYTE 0x6e 5   // Large swords
			SET_BG2_PROFICIENCY ~PROFICIENCY2WEAPON~ 2
			REMOVE_CRE_ITEM ~arow11~ ~arow15~ ~ax1h11~ ~blun33~ ~bow04~ ~bow16~ ~bow17~ ~gorcamb~ ~halb02~ ~shld06~ ~shld29~ ~staf18~ ~sw1h01~ ~sw1h02~ ~sw1h42~ ~sw1h72~ ~sw1h73~ ~sw1h74~ ~sw2h02~ ~sw2h08~ ~sw2h11~ ~telswd~
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~cambion1~ = 1) BEGIN
				REPLACE_CRE_ITEM ~sw1h33a~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
				REPLACE_CRE_ITEM ~sw1h33a~ #0 #0 #0 ~NONE~ ~SHIELD~
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~illcamb~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~illcamb2~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~kdemon01~ = 1) BEGIN
				REPLACE_CRE_ITEM ~genscim~ #0 #0 #0 ~NONE~ ~WEAPON2~ EQUIP
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~cambion2~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~idemon01~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~idemon02~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~nscamb01~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~telcam1~ = 1) BEGIN
				REPLACE_CRE_ITEM ~sw1h22~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
				REPLACE_CRE_ITEM ~sw1h20~ #0 #0 #0 ~NONE~ ~SHIELD~
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~fincamb2~ = 1) BEGIN
				REPLACE_CRE_ITEM ~sw1h52~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
				REPLACE_CRE_ITEM ~sw1h22~ #0 #0 #0 ~NONE~ ~SHIELD~
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~fincamb2~ = 1) BEGIN
				REPLACE_CRE_ITEM ~dagg16~ #10 #0 #0 ~NONE~ ~WEAPON2~
			END
			// Aesgareth
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~gorcamb~ = 1) BEGIN
				REPLACE_CRE_ITEM ~sw1h66~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
				REPLACE_CRE_ITEM ~sharswd~ #0 #0 #0 ~NONE~ ~SHIELD~
			END
		END
		WRITE_BYTE 0x237 1      // Sex (male)
		WRITE_BYTE 0x271 1      // General (humanoid)
		READ_BYTE  0x275 gd     // Gender
		// If not summoned or illusionary
		PATCH_IF (gd != 6) AND (gd != 7) AND (gd != 9) BEGIN
			WRITE_BYTE 0x275 1  // male
		END
		PATCH_IF SLONG_AT BATTLE_CRY1 < 1 BEGIN
			SAY BATTLE_CRY1 ~~ [t-isa01a]
			SAY BATTLE_CRY2 ~~ [t-isa01b]
			SAY BATTLE_CRY3 ~~ [t-isa04b]
			SAY BATTLE_CRY4 ~~ [t-isa05b]
			SAY BATTLE_CRY5 ~~ [t-isa09b]
		END
		PATCH_IF SLONG_AT ATTACK1 < 1 BEGIN
			SAY ATTACK1 ~~ [t-isa05a]
		END
		PATCH_IF SLONG_AT DAMAGE < 1 BEGIN
			SAY DAMAGE ~~ [t-isa07]
		END
		PATCH_IF SLONG_AT DYING < 1 BEGIN
			SAY DYING ~~ [t-isa09a]
		END
		PATCH_IF SLONG_AT SELECT_COMMON1 < 1 BEGIN
			SAY SELECT_COMMON1 ~~ [t-isa02a]
			SAY SELECT_COMMON2 ~~ [t-isa02b]
			SAY SELECT_COMMON3 ~~ [t-isa04a]
			SAY SELECT_COMMON4 ~~ [t-isa08]
		END
		PATCH_IF SLONG_AT CRITICAL_MISS < 1 BEGIN
			SAY CRITICAL_MISS ~~ [t-isa03a]
		END
		PATCH_IF SLONG_AT SPELL_DISRUPTED < 1 BEGIN
			SAY SPELL_DISRUPTED ~~ [t-isa03b]
		END
	END

BUT_ONLY

VERBOSE
