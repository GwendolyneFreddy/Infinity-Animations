/* ========================================================== *
 *  INFINITY ANIMATIONS : Saved Game Animation Fixer routine  *
 * ========================================================== */

INCLUDE ~infinityanimations/lib/t-animfix.tpa~

OUTER_SPRINT t06 @98	// Patch
OUTER_SPRINT t07 @99	// [Y/N]?
OUTER_SPRINT t10 @102	// Updating .gam file in
OUTER_SPRINT t11 @103	// Updating .sav file in

ACTION_FOR_EACH dir IN ~save~ ~mpsave~ BEGIN
	ACTION_IF (DIRECTORY_EXISTS ~%dir%~) BEGIN
		ACTION_CLEAR_ARRAY t-sv
		GET_DIRECTORY_ARRAY t-sv ~%dir%~ ~~

		ACTION_PHP_EACH t-sv AS t-from => t-to BEGIN
//			PRINT ~Patch %t-to% [Y/N]?~
			PRINT ~%t06% %t-to% %t07%~ // Patch [file] [Y/N]?
			ACTION_READLN t-rds
			ACTION_IF (~%t-rds%~ STRING_EQUAL_CASE ~y~ = 1) BEGIN
				ACTION_IF FILE_EXISTS ~%t-to%/baldur.gam~ BEGIN
//					PRINT ~Updating .gam file in %t-to% ...~
					PRINT ~%t10% %t-to% ...~ // Updating .gam file
					COPY ~%t-to%/baldur.gam~ ~%t-to%/baldur.gam~
						PATCH_IF SOURCE_SIZE > 0xb3 BEGIN
							SPRINT t-sav ~baldur.gam~
							SPRINT t-xt ~gam~
							READ_LONG 0x20 t-pf								// Party members offset
							READ_LONG 0x24 t-pc								// Party members count
							FOR (i = 0 ; i < t-pc ; i += 1) BEGIN
								READ_LONG (t-pf + i * 0x160 + 4) t-cf		// CRE file offset
								READ_ASCII (t-pf + i * 0x160 + 0xc) t-cn	// CRE file
								LPM t-animfix
							END
							READ_LONG 0x30 t-pf								// Non-party members offset
							READ_LONG 0x34 t-pc								// Non-party members count
							FOR (i = 0 ; i < t-pc ; i += 1) BEGIN
								READ_LONG (t-pf + i * 0x160 + 4) t-cf		// CRE file offset
								READ_ASCII (t-pf + i * 0x160 + 0xc) t-cn	// CRE file
								LPM t-animfix
							END
						END
					BUT_ONLY
				END

				ACTION_IF FILE_EXISTS ~%t-to%/baldur.sav~ BEGIN
//					PRINT ~Updating .sav file in %t-to% ...~
					PRINT ~%t11% in %t-to% ...~	// Updating .sav file
					COPY ~%t-to%/baldur.sav~ ~%t-to%/baldur.sav~
						EDIT_SAV_FILE 1 BEGIN
							SPRINT t-sav ~%SAV_FILE%~
							TO_LOWER t-sav
							lnd = STRING_LENGTH ~%t-sav%~
							INNER_PATCH ~%t-sav%~ BEGIN
								READ_ASCII (lnd - 4) t-fs (1)								// Read 4th character from end
								PATCH_IF (~%t-fs%~ STRING_EQUAL ~.~ = 0) BEGIN				// If not a dot
									READ_ASCII (lnd - 4) t-xt (3)							// Read extension
									READ_ASCII 0 t-sav (lnd - 1)							// Trim trailing null
								END ELSE BEGIN
									READ_ASCII (lnd - 3) t-xt (3)							// Read extension
								END
							END
							PATCH_IF (~%t-xt%~ STRING_EQUAL ~are~ = 1) BEGIN
								READ_LONG 0x54 t-pf											// Actors offset
								READ_SHORT 0x58 t-pc										// Actors count
								FOR (i = 0 ; i < t-pc ; i += 1) BEGIN						// Cycle through .sav actors
									READ_BYTE (t-pf + i * 0x110 + 0x28) t-fl				// Flags
									PATCH_IF ((t-fl BAND 0b00000001) = 0b00000000) BEGIN	// If CRE embedded
										READ_ASCII (t-pf + i * 0x110 + 0x80) t-cn			// CRE file
										READ_LONG (t-pf + i * 0x110 + 0x88) t-cf			// CRE file offset
										LPM t-animfix
									END
								END
							END
						END
					BUT_ONLY
				END
			END
		END
	END ELSE BEGIN
		MKDIR ~%dir%~
	END
END
