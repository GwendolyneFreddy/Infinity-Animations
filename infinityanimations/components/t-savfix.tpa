/* ========================================================================================================================== *
 *  v 6.0.0                                                                                                                   *
 *          S A V E D    G A M E S    A N I M A T I O N S    F I X E R    C O M P O N E N T    ( 9 9 0 0 - 9 9 1 0 )          *
 *                                                                                                                            *
 * ========================================================================================================================== *
 *  This component patches any changed creature animation in saved games to prevent game from crashing.                       *
 * ========================================================================================================================== *
 *  change-log:                                                                                                               *
 *  -----------                                                                                                               *
 *      - Used new %low_0xnnnn% variables to define former _LOW animations ID removed by IA.                                  *
 *      - Re-formated t-savlog.txt file entries to be more friendly readable.                                                 *
 * ========================================================================================================================== */

PRINT @9902 // ~Patching modified animations in saved games (this may take a while)...~
SILENT


/* ================= *
 *  Defines Prompts  *
 * ================= */
OUTER_SPRINT t06 @1216  // ~Patch~
OUTER_SPRINT t07 @1217  // ~[Y/N]?~
OUTER_SPRINT t10 @1220  // ~Updating .gam file in~
OUTER_SPRINT t11 @1221  // ~Updating .sav file in~


/* ==================================================== *
 *  Defines former _LOW animations slots removed by IA  *
 * ==================================================== */
LAM ~gw_set_low_anim_names~
INCLUDE ~infinityanimations/lib/t-animfix.tpa~

OUTER_SPRINT list_savlog ""


/* ========= *
 *  Patches  *
 * ========= */
ACTION_FOR_EACH dir IN ~save~ ~mpsave~ BEGIN

	ACTION_IF (DIRECTORY_EXISTS ~%dir%~) BEGIN

		ACTION_CLEAR_ARRAY  t-saves
		GET_DIRECTORY_ARRAY t-saves ~%dir%~ ~~

		ACTION_PHP_EACH t-saves AS t-from => t-to BEGIN

			PRINT ~%t06% %t-to% %t07%~ // Patch [file] [Y/N]?
			ACTION_READLN t-rds
			SILENT

			ACTION_IF (~%t-rds%~ STRING_EQUAL_CASE ~y~ = 1) BEGIN

				// Updating .gam file in %t-to%
				// ----------------------------
				ACTION_IF FILE_EXISTS ~%t-to%/baldur.gam~ BEGIN

					PRINT ~%t10% %t-to% ...~
					SILENT

					COPY ~%t-to%/baldur.gam~ ~%t-to%/baldur.gam~
						PATCH_IF SOURCE_SIZE > 0xb3 BEGIN
							SPRINT t-sav ~baldur.gam~
							SPRINT t-xt ~gam~
							READ_LONG 0x20 t-pf   // Party members offset
							READ_LONG 0x24 t-pc   // Party members count
							// Cycle through Party members
							FOR (i = 0 ; i < t-pc ; i += 1) BEGIN
								READ_LONG  (t-pf + i * 0x160 + 4)   t-cre_off    // CRE file offset
								READ_ASCII (t-pf + i * 0x160 + 0xc) t-cre_file   // CRE file
								LPM ~t-animfix~
							END
							READ_LONG 0x30 t-pf   // Non-party members offset
							READ_LONG 0x34 t-pc   // Non-party members count
							// Cycle through Non-party members
							FOR (i = 0 ; i < t-pc ; i += 1) BEGIN
								READ_LONG  (t-pf + i * 0x160 + 4)   t-cre_off    // CRE file offset
								READ_ASCII (t-pf + i * 0x160 + 0xc) t-cre_file   // CRE file
								LPM ~t-animfix~
							END
						END
					BUT_ONLY

				END

				// Updating .sav file in %t-to%
				// ----------------------------
				ACTION_IF FILE_EXISTS ~%t-to%/baldur.sav~ BEGIN

					PRINT ~%t11% %t-to% ...~
					SILENT

					COPY ~%t-to%/baldur.sav~ ~%t-to%/baldur.sav~
						EDIT_SAV_FILE 1 BEGIN
							SPRINT t-sav ~%SAV_FILE%~
							TO_LOWER t-sav
							lnd = STRING_LENGTH ~%t-sav%~
							INNER_PATCH ~%t-sav%~ BEGIN
								READ_ASCII (lnd - 4) t-fs (1)      // Read 4th character from end
								// If not a dot
								PATCH_IF (~%t-fs%~ STRING_EQUAL ~.~ = 0) BEGIN
									READ_ASCII (lnd - 4) t-xt (3)  // Read extension
									READ_ASCII 0 t-sav (lnd - 1)   // Trim trailing null
								END ELSE BEGIN
									READ_ASCII (lnd - 3) t-xt (3)  // Read extension
								END
							END
							PATCH_IF (~%t-xt%~ STRING_EQUAL ~are~ = 1) BEGIN
								READ_LONG  0x54 t-pf   // Actors offset
								READ_SHORT 0x58 t-pc   // Actors count
								// Cycle through .sav actors
								FOR (i = 0 ; i < t-pc ; i += 1) BEGIN
									READ_BYTE (t-pf + i * 0x110 + 0x28) t-flag            // Flags
									// If CRE embedded
									PATCH_IF ((t-flag BAND 0b00000001) = 0b00000000) BEGIN
										READ_ASCII (t-pf + i * 0x110 + 0x80) t-cre_file   // CRE file
										READ_LONG  (t-pf + i * 0x110 + 0x88) t-cre_off    // CRE file offset
										LPM ~t-animfix~
									END
								END
							END
						END
					BUT_ONLY

				END

			END // of ACTION_IF (~%t-rds%~ STRING_EQUAL_CASE ~y~ = 1)

		END // of ACTION_IF (DIRECTORY_EXISTS ~%dir%~)

	END ELSE BEGIN

		MKDIR ~%dir%~

	END

END

APPEND_OUTER ~infinityanimations/log/t-savlog.txt~ ~%list_savlog%~
