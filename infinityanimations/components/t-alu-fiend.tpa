/* ============================================================================================================================================================== *
 *  v 6.0.0                                                                                                                                                       *
 *                               A L U - F I E N D S   G E T   M A D A E   A N I M A T I O N   C O M P O N E N T   ( 2 6 0 - 2 8 0 )                              *
 *                                                                                                                                                                *
 * ============================================================================================================================================================== *
 *  This component assigns Alu-Fiends creatures the Madae animation. Weapon changes necessary as Madae animation displays hardcoded morningstar.                  *
 * ============================================================================================================================================================== *
 *  change-log:                                                                                                                                                   *
 *  -----------                                                                                                                                                   *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_alu_fiends_arrays.tph library) for easier maintenance.                        *
 *      - Rewrote animation ID patching to solve EE games compatibility: replaced WRITE_SHORT 0x28 0x55243 with WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~MADAE~).  *
 *      - Removed deprecated Female Tieflings patching process.                                                                                                   *
 * ============================================================================================================================================================== */

SILENT

COPY_EXISTING ~%nb%.cre~ ~override~

	PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
	PATCH_IF valid BEGIN
		PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
		PATCH_IF rgf = 1 BEGIN
			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~MADAE~) // original 0x5243 - EE 0xe277
			WRITE_BYTE 0x73 5   // Spiked
			REMOVE_CRE_ITEM ~bow02~ ~dagg02~ ~sharswd~ ~staf07~ ~staf17~ ~staf19~ ~sw1h09~ ~sw1h25~ ~sw1h52~ ~sw1h66~ ~sw1h74~
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~goralu01~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~teltief2~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~yugalu01~ = 1) BEGIN
				REPLACE_CRE_ITEM ~blun15~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~alufie01~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~finaluf~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~nsaluf01~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~telalu1~ = 1) BEGIN
				REPLACE_CRE_ITEM ~blun34~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
			END
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~alufie02~ = 1) OR (~%SOURCE_RES%~ STRING_EQUAL_CASE ~telsuc1~ = 1) BEGIN
				REPLACE_CRE_ITEM ~bow99~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED
			END
			// Xei Win Toh
			PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~gorwom06~ = 1) BEGIN
				SET_BG2_PROFICIENCY ~PROFICIENCY2WEAPON~ 2
				REPLACE_CRE_ITEM ~gorwom4~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
				REPLACE_CRE_ITEM ~blun07~ #0 #0 #0 ~NONE~ ~SHIELD~
			END
		END
		WRITE_BYTE 0x237 2      // Sex (female)
		WRITE_BYTE 0x271 1      // General (humanoid)
		READ_BYTE  0x275 gd     // Gender
		// If not summoned or illusionary
		PATCH_IF (gd != 6) AND (gd != 7) AND (gd != 9) BEGIN
			WRITE_BYTE 0x275 2  // female
		END
		// If an alu-fiend
		PATCH_IF SLONG_AT BATTLE_CRY1 < 1 BEGIN
			SAY BATTLE_CRY1 ~~ [t-mad02a]
			SAY BATTLE_CRY2 ~~ [t-mad02b]
			SAY BATTLE_CRY3 ~~ [t-mad03c]
			SAY BATTLE_CRY4 ~~ [t-mad04a]
			SAY BATTLE_CRY5 ~~ [t-mad05a]
		END
		PATCH_IF SLONG_AT ATTACK1 < 1 BEGIN
			SAY ATTACK1 ~~ [t-mad05c]
		END
		PATCH_IF SLONG_AT DAMAGE < 1 BEGIN
			SAY DAMAGE ~~ [t-mad08a]
		END
		PATCH_IF SLONG_AT DYING < 1 BEGIN
			SAY DYING ~~ [t-mad09c]
		END
		PATCH_IF SLONG_AT SELECT_COMMON1 < 1 BEGIN
			SAY SELECT_COMMON1 ~~ [t-mad01a]
			SAY SELECT_COMMON2 ~~ [t-mad01b]
			SAY SELECT_COMMON3 ~~ [t-mad04b]
			SAY SELECT_COMMON4 ~~ [t-mad05b]
		END
		PATCH_IF SLONG_AT CRITICAL_MISS < 1 BEGIN
			SAY CRITICAL_MISS ~~ [t-mad03a]
		END
		PATCH_IF SLONG_AT SPELL_DISRUPTED < 1 BEGIN
			SAY SPELL_DISRUPTED ~~ [t-mad08b]
		END
	END

BUT_ONLY

VERBOSE
