/* ====================================================================================================================================================================== *
 *  v 6.0.0                                                                                                                                                               *
 *                                                  M O R E   N W N   A N I M A T I O N S   C O M P O N E N T   ( 6 5 0 )                                                 *
 *                                                                                                                                                                        *
 * ====================================================================================================================================================================== *
 *  This component assigns some creatures distinctive NWN animations.                                                                                                     *
 * ====================================================================================================================================================================== *
 *  change-log:                                                                                                                                                           *
 *  -----------                                                                                                                                                           *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_more_nwn_anim_arrays.tph library) for easier maintenance.                             *
 *      - Rewrote animation ID patching to solve EE games compatibility: replaced WRITE_SHORT 0x28 hexcode with WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~animation name~)  *
 *        whenever relevant.                                                                                                                                              *
 * ====================================================================================================================================================================== */

/* ----------------------------------------------------------------------------------- *
 *                            CREATURES                                          line  *
 * ----------------------------------------------------------------------------------- *
 *  Scythe Wielders get the amel_pc_nwn animation                              #   36  *
 *  Bombardier Beetle (BP) gets the beetle_bombardier_nwn animation            #   64  *
 *  Greater Basilisks get the basilisk_nwn animation                           #   80  *
 *  Modrons get the modron_round animation                                     #  101  *
 *  Displacer Beasts (AjocMod) get the displacer_beast animation               #  129  *
 *  Fire Giant Females get the giant_fire_female animation                     #  161  *
 *  Frost Giant Females get the giant_frost_female animation                   #  186  *
 * ----------------------------------------------------------------------------------- */

SILENT

/* ============================== *
 *  Loads creatures lists arrays  *
 * ============================== */
INCLUDE ~%MOD_FOLDER%/arrays/gw_ia_more_nwn_anims_arrays.tph~


/* =============================================== *
 *  Scythe Wielders get the amel_pc_nwn animation  *
 * =============================================== */
ACTION_PHP_EACH GW_amel_pc_nwn AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~AMEL_PC_NWN~) // original 0x526d - EE depends on installation
				SAY INITIAL_MEETING @10651  // What do you want? [t-aml02a]
				SAY BATTLE_CRY1 @10652      // Taste steel! [t-aml01a]
				SAY BATTLE_CRY2 @10653      // Die as you must! [t-aml01b]
				SAY DAMAGE ~~ [t-aml08]
				SAY DYING ~~ [t-aml09]
				SAY SELECT_COMMON1 @10651   // What do you want? [t-aml02a]
				SAY SELECT_COMMON2 @10654   // Yes? [t-aml02b]
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_amel_pc_nwn


/* ================================================================= *
 *  Bombardier Beetle (BP) gets the beetle_bombardier_nwn animation  *
 * ================================================================= */
ACTION_IF FILE_EXISTS_IN_GAME ~beetbomb.cre~ BEGIN

	COPY_EXISTING ~beetbomb.cre~ ~override~
		PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~BEETLE_BOMBARDIER_NWN~) // original 0x5291 - EE depends on installation
			WRITE_BYTE 0x272 104    // Race (crawler)
			WRITE_BYTE 0x273 108    // Class (crawler)
		END
	BUT_ONLY

END


/* ================================================== *
 *  Greater Basilisks get the basilisk_nwn animation  *
 * ================================================== */
ACTION_PHP_EACH GW_basilisk_nwn AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~BASILISK_NWN~) // original 0x557b - EE depends on installation
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_basilisk_nwn


/* ======================================== *
 *  Modrons get the modron_round animation  *
 * ======================================== */
ACTION_PHP_EACH GW_modron_round AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~MODRON_ROUND~) // original 0x5b02 - EE depends on installation
				SAY BATTLE_CRY1 ~~ [t-mod01a]
				SAY BATTLE_CRY2 ~~ [t-mod01b]
				SAY ATTACK1 ~~ [t-mod03]
				SAY DAMAGE ~~ [t-mod07]
				SAY DYING ~~ [t-mod09]
				SAY SELECT_COMMON1 ~~ [t-mod02a]
				SAY SELECT_COMMON2 ~~ [t-mod02b]
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_modron_round


/* ============================================================== *
 *  Displacer Beasts (AjocMod) get the displacer_beast animation  *
 * ============================================================== */
ACTION_PHP_EACH GW_displacer_beast AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~DISPLACER_BEAST~) // original 0x5b07 - EE depends on installation
				SAY BATTLE_CRY1 ~~ [t-dis01a]
				SAY BATTLE_CRY2 ~~ [t-dis01b]
				SAY BATTLE_CRY3 ~~ [t-dis01c]
				SAY ATTACK1 ~~ [t-dis03]
				SAY DAMAGE ~~ [t-dis07]
				SAY DYING ~~ [t-dis09]
				SAY SELECT_COMMON1 ~~ [t-dis02a]
				SAY SELECT_COMMON2 ~~ [t-dis02b]
				SAY SELECT_COMMON3 ~~ [t-dis02c]
				SAY CRITICAL_MISS ~~ [t-dis05]
				SAY SPELL_DISRUPTED ~~ [t-dis06]
				END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_displacer_beast


/* ======================================================== *
 *  Fire Giant Females get the giant_fire_female animation  *
 * ======================================================== */
ACTION_PHP_EACH GW_giant_fire_female AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~GIANT_FIRE_FEMALE~) // original 0x5c03 - EE depends on installation
				WRITE_BYTE 0x237 2      // Sex (female)
				WRITE_BYTE 0x271 5      // General (gianthumanoid)
				WRITE_BYTE 0x275 2      // Gender (female)
				WRITE_BYTE 0x27b 19     // Alignment (lawful evil by definition)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_giant_fire_female


/* ========================================================== *
 *  Frost Giant Females get the giant_frost_female animation  *
 * ========================================================== */
ACTION_PHP_EACH GW_giant_frost_female AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~GIANT_FROST_FEMALE~) // original 0x5c04 - EE depends on installation
				WRITE_BYTE 0x237 2  // Sex (female)
				WRITE_BYTE 0x271 5  // General (gianthumanoid)
				WRITE_BYTE 0x275 2  // Gender (female)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_giant_frost_female

