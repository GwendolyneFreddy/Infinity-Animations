/* ================================================================================================================================================================================== *
 *  v 6.0.0                                                                                                                                                                           *
 *                                                    H U M A N O I D   A N I M A T I O N   F I X E S   C O M P O N E N T   ( 25 )                                                    *
 *                                                                                                                                                                                    *
 * ================================================================================================================================================================================== *
 *  This component fixes humanoid creature files that have wrong animation slots.                                                                                                     *
 * ================================================================================================================================================================================== *
 *  change-log:                                                                                                                                                                       *
 *  -----------                                                                                                                                                                       *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_humanoid_fixes_arrays.tph library) for easier maintenance.                                        *
 *      - Tutu variables: replaced specific %tsx% ia variables with more compatible %tutu_scriptx% variables and added missing tutu variable for BG1 kent and sailned files.          *
 *      - Integrated Lollorian's patch to correct loops: male elf mages were not patched ( http://www.shsforums.net/topic/43531-ia-comments/page-3#entry549298 ).                     *
 *      - Replaced WRITE_SHORT 0x28 with WRITE_LONG 0x28.                                                                                                                             *
 * ================================================================================================================================================================================== */

/* ----------------------------------------------------------------------------- *
 *                             CREATURES                                   line  *
 * ----------------------------------------------------------------------------- *
 *  Male human fighters get the fighter instead of cleric animation      #   43  *
 *  Male elf fighters get the fighter instead of cleric animation        #   79  *
 *  Male halfling fighters get the fighter instead of cleric animation   #  102  *
 *  Female human fighters get the fighter instead of cleric animation    #  123  *
 *  Male human mages get the mage instead of cleric animation            #  150  *
 *  Male elf mages get the mage instead of cleric animation              #  173  *
 *  Male human thieves get the thief instead of fighter animation        #  198  *
 *  Female human thieves get the thief instead of fighter animation      #  219  *
 *  Male human monks get the monk instead of cleric animation            #  238  *
 *  Male human pirates get the pirate animation                          #  263  *
 *  Male human sailors get the sailor_man animation                      #  301  *
 *  Amnian soldiers get the amnian_soldier animation                     #  327  *
 *  Male human Shadow Thieves get the shadow_thief animation             #  358  *
 * ----------------------------------------------------------------------------- */

SILENT

/* ============================== *
 *  Loads creatures lists arrays  *
 * ============================== */
INCLUDE ~%MOD_FOLDER%/arrays/gw_ia_humanoid_fixes_arrays.tph~


/* ======================================================================= *
 *  Male Human Fighters get the fighter_male_human animation (was cleric)  *
 * ======================================================================= */
ACTION_PHP_EACH GW_male_human_fighters AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6100    // Animation ID (fighter_male_human)
				// Special case: Brega
				PATCH_IF (~%SOURCE_RES%~ STRING_EQUAL_CASE ~habrega~ = 1) BEGIN
					WRITE_BYTE 0x238 18   // Strength (was 17 - has a composite bow)
					WRITE_BYTE 0x239 54   // STR % bonus (he's a fighter after all)
				END
				WRITE_BYTE 0x237 1        // Sex (male)
				// If invalid class
				PATCH_IF (BYTE_AT 0x273 = 4) OR (BYTE_AT 0x273 > 200) BEGIN
					WRITE_BYTE 0x273 2    // Class (fighter)
				END
				WRITE_BYTE 0x275 1        // Gender (male)
				// If no alignment
				PATCH_IF BYTE_AT 0x27b = 0 BEGIN
					WRITE_BYTE 0x27b 0x12 // Alignment (lawful_neutral)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_human_fighters


/* =================================================================== *
 *  Male Elf Fighters get the fighter_male_elf animation (was cleric)  *
 * =================================================================== */
ACTION_PHP_EACH GW_male_elf_fighters AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6101  // Animation ID (fighter_male_elf)
				WRITE_BYTE 0x237 1      // Sex (male)
				WRITE_BYTE 0x275 1      // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_elf_fighters


/* ============================================================================= *
 *  Male Halfling Fighters get the fighter_male_halfling animation (was cleric)  *
 * ============================================================================= */
ACTION_PHP_EACH GW_male_halfling_fighters AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6103  // Animation ID (fighter_male_halfling)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_halfling_fighters


/* =========================================================================== *
 *  Female Human Fighters get the fighter_female_human animation (was cleric)  *
 * =========================================================================== */
ACTION_PHP_EACH GW_female_human_fighters AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6110  // Animation ID (fighter_female_human)
				WRITE_BYTE 0x237 2      // Sex (female)
				// If thief
				PATCH_IF BYTE_AT 0x273 = 4 BEGIN
					WRITE_BYTE 0x273 2  // Class (fighter)
				END
				WRITE_BYTE 0x275 2      // Gender (female)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_female_human_fighters


/* ================================================================= *
 *  Male Human Mages get the mage_male_human animation (was cleric)  *
 * ================================================================= */
ACTION_PHP_EACH GW_male_human_mages AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6200 // Animation ID (mage_male_human)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_human_mages


/* ============================================================= *
 *  Male Elf Mages get the mage_male_elf animation (was cleric)  *
 * ============================================================= */
ACTION_PHP_EACH GW_male_elf_mages AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6201 // Animation ID (mage_male_elf) - was cleric
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x272 2     // Race (elf)
				WRITE_BYTE 0x273 1     // Class (mage)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_elf_mages


/* ===================================================================== *
 *  Male Human Thieves get the thief_male_human animation (was fighter)  *
 * ===================================================================== */
// Crewmate
ACTION_FOR_EACH t-bcre IN ~%tutu_var%crew4~ ~%tutu_var%crew7~ BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6300 // Animation ID (thief_male_human)
			END
		BUT_ONLY

	END

END


/* ========================================================================= *
 *  Female Human Thieves get the thief_female_human animation (was fighter)  *
 * ========================================================================= */
// Crewmate
ACTION_IF FILE_EXISTS_IN_GAME ~%tutu_var%crew2.cre~ BEGIN 

	COPY_EXISTING ~%tutu_var%crew2.cre~ ~override~
		PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
		PATCH_IF valid BEGIN
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
			WRITE_LONG 0x28 0x6310 // Animation ID (thief_female_human)
			WRITE_BYTE 0x237 2     // Sex (female, was male)
			WRITE_BYTE 0x275 2     // Gender (female, was male)
		END
	BUT_ONLY

END


/* ================================================================= *
 *  Male Human Monks get the monk_male_human animation (was cleric)  *
 * ================================================================= */
ACTION_PHP_EACH GW_male_human_monks AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x6500  // Animation ID (monk_male_human)
				// If cleric
				PATCH_IF BYTE_AT 0x273 = 3 BEGIN
					WRITE_BYTE 0x273 0x14 // Class (monk)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_human_monks


/* ============================================= *
 *  Male Human Pirates get the pirate animation  *
 * ============================================= */
ACTION_PHP_EACH GW_male_human_pirates AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f24  // Animation ID (pirate)
				WRITE_BYTE 0x237 1      // Sex (male)
				// If not halforc
				PATCH_IF BYTE_AT 0x272 != 7 BEGIN
					WRITE_BYTE 0x272 1  // Race (human)
				END
				// If invalid class
				PATCH_IF (BYTE_AT 0x273 > 0x14) OR (BYTE_AT 0x273 != 0x9b) BEGIN
					WRITE_BYTE 0x273 4  // Class (thief)
				END
				// If not summoned
				PATCH_IF BYTE_AT 0x275 != 6 BEGIN
					WRITE_BYTE 0x275 1  // Gender (male)
				END
				// If no alignment
				PATCH_IF BYTE_AT 0x27b = 0 BEGIN
					WRITE_BYTE 0x27b 0x32 // Alignment (chaotic_neutral)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_human_pirates


/* =========================================== *
 *  Male Sailors get the sailor_man animation  *
 * =========================================== */
ACTION_PHP_EACH GW_male_sailors AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f2a  // Animation ID (sailor_man)
				WRITE_BYTE 0x237 1      // Sex (male)
				// If not summoned
				PATCH_IF BYTE_AT 0x275 != 6 BEGIN
					WRITE_BYTE 0x275 1  // Gender (male)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_male_sailors


/* =============================================================== *
 *  Amnian Soldiers get the amnian_soldier animation (was cleric)  *
 * =============================================================== */
ACTION_PHP_EACH GW_amnian_soldiers AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f2c  // Animation ID (amnian_soldier)
				WRITE_BYTE 0x237 1      // Sex (male)
				// If invalid class
				PATCH_IF (BYTE_AT 0x273 = 4) OR (BYTE_AT 0x273 > 200) BEGIN
					WRITE_BYTE 0x273 2  // Class (fighter)
				END
				WRITE_BYTE 0x275 1      // Gender (male)
				// If no alignment
				PATCH_IF BYTE_AT 0x27b = 0 BEGIN
					WRITE_BYTE 0x27b 0x12 // Alignment (lawful_neutral)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_amnian_soldiers


/* ========================================================== *
 *  Male human Shadow Thieves get the shadow_thief animation  *
 * ========================================================== */
ACTION_PHP_EACH GW_shadow_thieves AS t-bcre => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%t-bcre%.cre~ BEGIN

		COPY_EXISTING ~%t-bcre%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f36 // Animation ID (shadow_thief)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_shadow_thieves
