/* ==================================================================================================================================== *
 *  v 6.0.0                                                                                                                             *
 *                         S K E L E T O N    W A R R I O R S    C O M P O N E N T    ( 4 1 0 - 4 1 5 - 4 2 0 )                         *
 *                                                                                                                                      *
 * ==================================================================================================================================== *
 *  This component assigns the chosen animation to all skeleton warriors.                                                               *
 * ==================================================================================================================================== *
 *  change-log:                                                                                                                         *
 *  -----------                                                                                                                         *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_skelwa_arrays.tph library) for easier maintenance.  *
 *      - Added Turambar's glowcheck.tpp patch ( http://www.shsforums.net/topic/45497-glowing-undead/page-2#entry496098 ).              *
 *      - Replaced WRITE_SHORT 0x28 with WRITE_LONG 0x28.                                                                               *
 * ==================================================================================================================================== */

PRINT @28 // ~Patching creature files...~
SILENT

/* ============================== *
 *  Loads creatures lists arrays  *
 * ============================== */
INCLUDE ~%MOD_FOLDER%/arrays/gw_ia_skelwa_arrays.tph~


/* ======= *
 *  Patch  *
 * ======= */
ACTION_PHP_EACH GW_skeleton_warriors AS nb => correct BEGIN

	  ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		SILENT

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 wk      // Animation ID
				WRITE_BYTE 0x237 4      // Sex (neither)
				WRITE_BYTE 0x271 255    // General (monster)
				WRITE_BYTE 0x272 115    // Race (skeleton)
				READ_BYTE  0x273 cs     // Class
				// If invalid class
				PATCH_IF cs > 20 BEGIN
				  WRITE_BYTE 0x273 135  // skeleton_warrior
				END
				READ_BYTE  0x275 gd     // Gender
				// If not summoned
				PATCH_IF gd != 6 BEGIN
				  WRITE_BYTE 0x275 4    // neither
				END
				PATCH_INCLUDE ~infinityanimations/lib/glowcheck.tpp~
			  END
		BUT_ONLY

	END

END

ACTION_CLEAR_ARRAY GW_skeleton_warriors
