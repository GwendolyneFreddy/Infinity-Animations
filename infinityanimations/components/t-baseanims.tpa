/* ====================================================================================================================================================================== *
 *  v 6.0.0                                                                                                                                                               *
 *                                               M O R E    B A S E    A N I M A T I O N S    C O M P O N E N T    ( 5 0 0 )                                              *
 *                                                                                                                                                                        *
 * ====================================================================================================================================================================== *
 *  This component assigns some creatures distinctive basic animations.                                                                                                   *
 * ====================================================================================================================================================================== *
 *  change-log:                                                                                                                                                           *
 *  -----------                                                                                                                                                           *
 *      - Externalized lists of creatures to be patched into arrays (built in gw_ia_more_base_anim_arrays.tph library) for easier maintenance.                            *
 *      - Moved BG2 content from More IWD2 Animations component (t-iwd2anims.tpa): Death Tyrants (#46), Ellesime Clones (#71), Fire Giants (#98 and #130).                *
 *      - Rewrote animation ID patching to solve EE games compatibility: replaced WRITE_SHORT 0x28 hexcode with WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~animation name~)  *
 *        whenever relevant.                                                                                                                                              *
 * ====================================================================================================================================================================== */

/* ------------------------------------------------------------------------------ *
 *                    CREATURES                                             line  *
 * ------------------------------------------------------------------------------ *
 *  Death Tyrants get the death_tyrant animation                          #   46  *
 *  Ellesime Clones get the ellesime_white animation                      #   71  *
 *  Fire Giants get the giant_fire animation                              #   98  *
 *  Greater fire giants and firbolgs get the giant_fire_2 animation       #  130  *
 *  Histachii get the histachii animation                                 #  161  *
 *  Brown Lizardmen get the lizard_man_elite animation                    #  185  *
 *  Lizardman Shamans get the lizard_man animation                        #  209  *
 *  Lizardman chiefs get the lizard_king animation                        #  235  *
 *  Trolls get the troll animation                                        #  261  *
 *  Blue Trolls get the troll_blue animation                              #  286  *
 *  Ice Trolls get the troll_ice animation                                #  323  *
 *  Small Trolls get the troll_small animation                            #  347  *
 *  Snow Trolls get the troll_snow animation                              #  371  *
 *  Umber Hulks get the umber_hulk animation                              #  398  *
 *  Elder Umber Hulks get the umber_hulk_elder (IWD) animation            #  425  *
 * ------------------------------------------------------------------------------ */

PRINT @28 // ~Patching creature files...~
SILENT

/* ============================== *
 *  Loads creatures lists arrays  *
 * ============================== */
INCLUDE ~%MOD_FOLDER%/arrays/gw_ia_more_base_anim_arrays.tph~


/* =============================================================================== *
 *  Death Tyrants get the death_tyrant animation                                   *
 * ------------------------------------------------------------------------------- *
 *  ==> As of v6.0.0: Moved from More IWD2 Animations component (t-iwd2anims.tpa)  *
 * =============================================================================== */
ACTION_PHP_EACH GW_death_tyrant AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~DEATH_TYRANT~) // original 0x5c00 - EE 0x7f46
				WRITE_BYTE 0x271 4  // General (undead)
				WRITE_BYTE 0x275 4  // Gender (neither)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_death_tyrant


/* =============================================================================== *
 *  Ellesime Clones get the ellesime_white animation                               *
 * ------------------------------------------------------------------------------- *
 *  ==> As of v6.0.0: Moved from More IWD2 Animations component (t-iwd2anims.tpa)  *
 * =============================================================================== */
ACTION_PHP_EACH GW_ellesime_white AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~ELLESIME_WHITE~) // original 0x5b00 - EE 0x7f44
				// If invalid class
				PATCH_IF (BYTE_AT 0x273 = 114) OR (BYTE_AT 0x273 = 255) BEGIN
					WRITE_BYTE 0x273 3  // Class (cleric)
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_ellesime_white


/* =============================================================================== *
 *  Fire Giants get the giant_fire animation                                       *
 * ------------------------------------------------------------------------------- *
 *  ==> As of v6.0.0: Moved from More IWD2 Animations component (t-iwd2anims.tpa)  *
 * =============================================================================== */
ACTION_PHP_EACH GW_giant_fire AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f3e  // Animation ID (giant_fire)
				WRITE_BYTE 0x237 1      // Sex (male)
				WRITE_BYTE 0x271 5      // General (gianthumanoid)
				WRITE_BYTE 0x272 142    // Race (giant)
				READ_BYTE  0x275 gd     // Gender
				// If not summoned or illusionary
				PATCH_IF (gd != 6) AND (gd != 7) BEGIN
					WRITE_BYTE 0x275 1  // Gender (male)
				END
				WRITE_BYTE 0x27b 19     // Alignment (lawful evil by definition)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_giant_fire


/* =============================================================================== *
 *  Greater Fire Giants and firbolgs get the giant_fire_2 animation                *
 * ------------------------------------------------------------------------------- *
 *  ==> As of v6.0.0: Moved from More IWD2 Animations component (t-iwd2anims.tpa)  *
 * =============================================================================== */
ACTION_PHP_EACH GW_giant_fire_2 AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 IDS_OF_SYMBOL (animate ~GIANT_FIRE_2~) // original 0x5c01 - EE 0x7f47
				WRITE_BYTE 0x237 1      // Sex (male)
				WRITE_BYTE 0x271 5      // General (gianthumanoid)
				READ_BYTE  0x275 gd     // Gender
				// If not summoned or illusionary
				PATCH_IF (gd != 6) AND (gd != 7) BEGIN
					WRITE_BYTE 0x275 1  // Gender (male)
				END
				WRITE_BYTE 0x27b 19     // Alignment (lawful evil by definition)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_giant_fire_2


/* ======================================= *
 *  Histachii get the histachii animation  *
 * ======================================= */
ACTION_PHP_EACH GW_histachii AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe040 // Animation ID (histachii)
				WRITE_BYTE 0x271 1     // General (humanoid)
				WRITE_BYTE 0x272 154   // Race (yuanti)
				WRITE_BYTE 0x273 2     // Class (fighter)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_histachii


/* ============================================================ *
 *  Brown Lizardmen get the lizard_man_elite (brown) animation  *
 * ============================================================ */
ACTION_PHP_EACH GW_lizardmen_brown AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe500 // Animation ID (lizard_man_elite)
				WRITE_BYTE 0x2f 114    // Skin color (YET_ANOTHER_BROWN)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x272 154   // Race (lizardman)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_lizardmen_brown


/* ======================================================== *
 *  Lizardman Shamans get the lizard_man (green) animation  *
 * ======================================================== */
ACTION_PHP_EACH GW_lizardmen_green AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe510 // Animation ID (lizard_man)
				WRITE_BYTE 0x2f 53     // Skin color (GREEN)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x271 1     // General (humanoid)
				WRITE_BYTE 0x272 154   // Race (lizardman)
				WRITE_BYTE 0x273 14    // Class (cleric_mage)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_lizardmen_green


/* ================================================ *
 *  Lizardman chiefs get the lizard_king animation  *
 * ================================================ */
ACTION_PHP_EACH GW_lizardmen_kings AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe520 // Animation ID (lizard_king)
				WRITE_BYTE 0x2f 36     // Skin color (LIGHT_DIRTY_YELLOW)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x271 1     // General (humanoid)
				WRITE_BYTE 0x272 154   // Race (lizardman)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_lizardmen_kings


/* ================================ *
 *  Trolls get the troll animation  *
 * ================================ */
ACTION_PHP_EACH GW_trolls AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f00 // Animation ID (troll)
				WRITE_BYTE 0x2f 86     // Skin color (CHROME_GREEN)
				WRITE_BYTE 0x237 1     // Sex (male)
				WRITE_BYTE 0x271 5     // General (gianthumanoid)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_trolls


/* ========================================== *
 *  Blue Trolls get the troll_blue animation  *
 * ========================================== */
ACTION_PHP_EACH GW_troll_blue AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe0b0 // Animation ID (troll_blue)
				WRITE_BYTE 0x2f 32     // Skin color (DARK_SEA_GREEN)
				READ_BYTE  0x237 sx    // Sex
				// If not female
				PATCH_IF sx != 2 BEGIN
					WRITE_BYTE 0x237 1 // male
				END
				READ_BYTE  0x271 gn    // General
				// If not undead
				PATCH_IF gn != 4 BEGIN
					WRITE_BYTE 0x271 5 // General (gianthumanoid)
				END
				READ_BYTE  0x275 gd    // Gender
				// If not female and not summoned
				PATCH_IF (gd != 2) AND (gd != 6) BEGIN
					WRITE_BYTE 0x275 1 // male
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_troll_blue


/* ======================================== *
 *  Ice Trolls get the troll_ice animation  *
 * ======================================== */
ACTION_PHP_EACH GW_troll_ice AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe0a0 // Animation ID (troll_ice)
				WRITE_BYTE 0x2f 71     // Skin color (FROST_BLUE)
				WRITE_BYTE 0x271 5     // General (gianthumanoid)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_troll_ice


/* ============================================ *
 *  Small Trolls get the troll_small animation  *
 * ============================================ */
ACTION_PHP_EACH GW_troll_small AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f0f // Animation ID (troll_small)
				WRITE_BYTE 0x2f 86     // Skin color (CHROME_GREEN)
				WRITE_BYTE 0x271 1     // General (humanoid)
				WRITE_BYTE 0x275 1     // Gender (male)
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_troll_small


/* ========================================== *
 *  Snow Trolls get the troll_snow animation  *
 * ========================================== */
ACTION_PHP_EACH GW_troll_snow AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe0c0 // Animation ID (troll_snow)
				WRITE_BYTE 0x2f 105    // Skin color (OFF_WHITE)
				WRITE_BYTE 0x271 5     // General (gianthumanoid)
				WRITE_BYTE 0x275 1     // Gender (male)
				LPF DELETE_CRE_EFFECT
					INT_VAR opcode_to_delete = 52 // Very bright by RGB
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_troll_snow


/* ========================================== *
 *  Umber Hulks get the umber_hulk animation  *
 * ========================================== */
ACTION_PHP_EACH GW_umber_hulk AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0x7f11 // Animation ID (umber_hulk)
				WRITE_BYTE 0x237 1     // male
				READ_BYTE  0x275 gd    // Gender
				// If not summoned
				PATCH_IF gd != 6 BEGIN
					WRITE_BYTE 0x275 1 // male
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_umber_hulk


/* ============================================================ *
 *  Elder Umber Hulks get the umber_hulk_elder (IWD) animation  *
 * ============================================================ */
ACTION_PHP_EACH GW_umber_hulk_elder AS nb => correct BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~%nb%.cre~ BEGIN

		COPY_EXISTING ~%nb%.cre~ ~override~
			PATCH_INCLUDE ~infinityanimations/lib/fj_cre_validity.tpp~
			PATCH_IF valid BEGIN
				PATCH_INCLUDE ~infinityanimations/lib/fj_cre_reindex.tpp~
				WRITE_LONG 0x28 0xe0d0 // Animation ID (umber_hulk_elder)
				WRITE_BYTE 0x237 1     // male
				READ_BYTE  0x275 gd    // Gender
				// If not summoned
				PATCH_IF gd != 6 BEGIN
					WRITE_BYTE 0x275 1 // male
				END
			END
		BUT_ONLY

	END

END
ACTION_CLEAR_ARRAY GW_umber_hulk_elder
